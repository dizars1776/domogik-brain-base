// Ce fichier comprend tous les enfants de DT_Bool

//////// Actions
// ##feature## contrôler des lumières ou prises

/* ##suggest## 
? .* allume.* ((?:la |le |l ).*)
@ allumer <star1>
*/

/* ##suggest## 
? .* (?:etein|eteigne).* ((?:la |le |l ).*)
@ eteindre <star1> 
*/
! array onoff = la lumiere|les lumieres|la lampe|les lampes|l eclairage|les eclairages|le luminaire|les luminaires|la prise|les prises
! array eteindre = etein|eteins|eteind|eteint|eteindre|eteignez
! array ouvrir = ouvre|ouvres|ouvrir|ouvrez

+ shortcut bool command * dev * val *
- <call>do_command fr_FR, <star1>, <star2>, <star3></call>

+ allume@premierGroupe [@pronom] *
@ shortcut bool command DT_Switch|DT_ColorRGBHexa dev <star1> val 1

+ allume@premierGroupe (@onoff) [@cible] *
@ shortcut bool command DT_Switch|DT_ColorRGBHexa dev <star2> val 1

+ (@eteindre|coupe@premierGroupe) [@pronom] *
@ shortcut bool command DT_Switch|DT_ColorRGBHexa dev <star2> val 0

+ (@eteindre|coupe@premierGroupe) (@onoff) [@cible] *
@ shortcut bool command DT_Switch|DT_ColorRGBHexa dev <star3> val 0



// note : volet et rideau au pluriel car il peut y en avoir 2 par ouvrant
! array openclose = la porte fenetre|la porte de garage|la porte|la fenetre|la baie vitree|la baie|le volet|les volets|la persienne|le rideau|les rideaux|le store|le velux
! array descendre = descend|descends|descendre|descendez

+ (@ouvrir|monte@premierGroupe) [@pronom] *
@ shortcut bool command DT_Switch|DT_OpenClose dev <star2> val 1

+ (@ouvrir|monte@premierGroupe) (@openclose) [@cible] *
@ shortcut bool command DT_Switch|DT_OpenClose dev <star3> val 1

+ (@descendre|ferme@premierGroupe) [@pronom] *
@ shortcut bool command DT_Switch|DT_OpenClose dev <star2> val 0

+ (@descendre|ferme@premierGroupe) (@openclose) [@cible] *
@ shortcut bool command DT_Switch|DT_OpenClose dev <star3> val 0



+ active@premierGroupe [@pronom] *
@ shortcut bool command DT_Enable dev <star1> val 1

+ desactive@premierGroupe [@pronom] *
@ shortcut bool command DT_Enable dev <star2> val 0


//////// Lumières/prises - Etat - 1 - status
! array switch = la prise|la lumiere

+ shortcut switch status *
* <set val=<call>get_sensor_value fr_FR, DT_Switch|DT_ColorRGBHexa, , <star></call>><get val> == inconnue => Je ne connais pas <star>
* <get val> == 0 => Eteind
* <get val> == 000000 => Eteind
- Allumé

+ comment est (@switch) [@cible] *
@ shortcut switch status <star2> 

+ quel est l etat [@cible] (@switch) [@cible] *
@ shortcut switch status <star2> 


//////// Lumières/prises - Etat - 2 - allumé ?

+ shortcut is switch on *
* <set val=<call>get_sensor_value fr_FR, DT_Switch|DT_ColorRGBHexa, , <star></call>><get val> == inconnue => Je ne connais pas <star>
* <get val> == 0 => Non
* <get val> == 000000 => Non
- Oui

+ est[-]ce que (@switch) est allume@e [@cible] *
@ shortcut is switch on <star2> 

+ est[-]ce que (@switch) [@cible] * est allume@e
@ shortcut is switch on <star2> 

+ est[-]ce que [@pronom] * est allume@e
@ shortcut is switch on <star1> 


//////// Lumières/prises - Etat - 3 - éteind ?

+ shortcut is switch off *
* <set val=<call>get_sensor_value fr_FR, DT_Switch|DT_ColorRGBHexa, , <star></call>><get val> == inconnue => Je ne connais pas <star>
* <get val> == 0 => Oui
* <get val> == 000000 => Oui
- Non

+ est[-]ce que (@switch) est (eteinte|coupe@e) [@cible] *
@ shortcut is switch off <star3> 

+ est[-]ce que (@switch) [@cible] * est (eteinte|coupe@e)
@ shortcut is switch off <star2> 

+ est[-]ce que [@pronom] * est (eteinte|coupe@e)
@ shortcut is switch off <star1> 





//////// Ouvertures - Etat - 1 - status

+ shortcut openclose status *
* <set val=<call>get_sensor_value fr_FR, DT_OpenClose, , <star></call>><get val> == inconnue => Je ne connais pas <star>
* <get val> == 0 => Ouvert
- Fermé

+ comment est (@openclose) [@cible] *
@ shortcut openclose status <star2> 

+ quel est l etat [@cible] (@openclose) [@cible] *
@ shortcut openclose status <star2> 


//////// Ouvertures - Etat - 2 - fermée (1) ?

+ shortcut is openclose closed *
* <set val=<call>get_sensor_value fr_FR, DT_OpenClose, , <star></call>><get val> == inconnue => Je ne connais pas <star>
* <get val> == 0 => Non
- Oui

+ est[-]ce que (@openclose) est ferme@e [@cible] *
@ shortcut is openclose closed <star2> 

+ est[-]ce que (@openclose) [@cible] * est ferme@e
@ shortcut is openclose closed <star2> 

+ est[-]ce que [@pronom] * est ferme@e
@ shortcut is openclose closed <star1> 


//////// Ouvertures - Etat - 3 - ouverte ?

+ shortcut is openclose open *
* <set val=<call>get_sensor_value fr_FR, DT_OpenClose, , <star></call>><get val> == inconnue => Je ne connais pas <star>
* <get val> == 0 => Oui
- Non

+ est[-]ce que (@openclose) est ouvert@e [@cible] *
@ shortcut is openclose open <star2> 

+ est[-]ce que (@openclose) [@cible] * est ouvert@e
@ shortcut is openclose open <star2> 

+ est[-]ce que [@pronom] * est ouvert@e
@ shortcut is openclose open <star1> 

